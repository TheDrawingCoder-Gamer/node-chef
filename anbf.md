```
document = *(function NL NL) function
function = title NL NL [comment] [ingredientList NL NL] [cookingTime NL NL] [temp NL NL] methodsStatement [NL NL servesStatement];
title      = name "." ;
comment    =  1*(1*(ALPHA / WSP / DIGIT) NL) NL ;
ingredientList = "Ingredients" (("." NL) / ":") ingredient [(NL / ",") *(ingredient (NL / ",")) ingredient)];
ingredient = [number [[measure] unit]] name;
measure = "heaped" / "level";
unit = "g" / "kg" / ("pinch" ["es"]) ;
unit /= "l" / "ml" / ("dash" ["es"]) ;
unit /= ("cup" ["s"]) / ("teaspoon" ["s"]) / ("tablespoon" ["s"]) ;
cookingTime = "Cooking time:" SP number SP (("hour" ["s"]) / ("minute" ["s"])) ".";
temp = "Pre-heat oven to" SP number SP "degrees Celsius" SP ["(gas mark" SP number ")"] ".";
methodsStatement = "Method" ("." NL / ":")  1*(action [NL]);
action = callStatement / takeStatement / putStatement / foldStatement;
action /= addStatement / removeStatement / combineStatement / divideStatement;
action /= addDryStatement / liquefyStatement / liquefyContentsStatement ;
action /=  stirStatment / stirIngredientStatement / mixStatement ;
action /= cleanStatement / pourStatement / refrigerateStatement ;
action /= loopStatement ;
servesStatement = "Serves" SP number ".";
callStatement = "Serve with" SP name "." ;
takeStatement = "Take" SP name SP "from refrigerator." ;
putStatement = "Put" SP name SP "into" SP mixingBowl "." ;
foldStatement = "Fold" SP name SP "into" SP mixingBowl "." ;
addStatement = "Add" SP name [SP "to" SP mixingBowl] "." ;
removeStatement = "Remove" SP name [SP "from" SP mixingBowl] "." ;
combineStatement = "Combine" SP name [SP "into" SP mixingBowl] "." ;
divideStatement = "Divide" SP name [SP "into" SP mixingBowl] "." ;
addDryStatement = "Add dry ingredients" [SP "to" SP mixingBowl] "." ;
liquefyStatement = ("Liquefy" / "Liquify") SP name "." ;
liquefyContentsStatement = ("Liquefy" / "Liquify") SP "contents of" SP mixingBowl "." ;
stirStatement = "Stir" SP [ mixingBowl SP] "for" SP number SP "minutes." ;
stirIngredientStatement = "Stir" SP name SP "into" SP mixingBowl "." ;
mixStatement = "Mix" SP [mixingBowl SP] "well." ;
cleanStatement = "Clean" SP mixingBowl "." ;
pourStatement = "Pour contents of the" SP mixingBowl SP "into" SP bakingDish "." ;
refrigerateStatement = "Refrigerate" [SP "for" SP number SP "hour" ["s"]] "." ;
loopStatement = terminal SP "the" SP name "." *( NL ( action / setAsideStatement) ) NL terminal SP ["the" SP name SP] "until" SP terminal ".";
setAsideStatement = "Set Aside."
mixingBowl = ("the" / ordinal ) SP "mixing bowl" ;
bakingDish = ("the" / ordinal) SP "baking dish" ;
name       = terminal [1*(SP terminal)] ;
terminal   = ALPHA 1*(ALPHA / DIGIT) ;
number = (("1" / "2" / "3" / "4" / "5" / "6" / "7" / "8" / "9") *(DIGIT));
ordinal = number ("th" / "st" / "nd" / "rd") ;
NL = [CR] LF ;
```
